integer
is_white(list map, integer x, integer y)
{
    integer p, w;
    data b;

    b = map[y];
    w = b[x >> 3];
    p = 1 << (7 - (x & 7));
    b[x >> 3] = w ^ p;

    !(w & p);
}

void
ant(integer x, integer y, integer d, list map)
{
    while (-1 < x && x < 100 && -1 < y && y < 100) {
        d += is_white(map, x, y) ? 3 : 1;
        d &= 3;

        if (d & 1) {
            y += (d & 2) - 1;
        } else {
            x += 1 - (d & 2);
        }
    }
}

integer
main(void)
{
    integer i;
    file f;
    list l;

    i = 100;
    while (i) {
        i -= 1;
        l_n_data(l, -1).run(13, 0);
    }

    ant(50, 50, 2, l);

    f.create("ant.pbm", 00644);

    f.text("P4\n100 100\n");
    l.ucall(f_data, 1, f);

    0;
}
