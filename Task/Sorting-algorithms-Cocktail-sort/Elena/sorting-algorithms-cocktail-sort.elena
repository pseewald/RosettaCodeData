import extensions.
import system'math.
import system'routines.

extension $op
{
    cocktailSort
    [
        var list := self clone.

        bool swapped  := true.
        while(swapped)
        [
            swapped := false.

            0 to(list length - 2) do(:i)
            [
                if (list[i]>list[i+1])
                [
                    list exchange(i,i+1).
                    swapped := true.
                ]
            ].
            ifnot (swapped)
            [
                ^ list
            ].
            swapped := false.

            (list length - 2) to(0) do(:i)
            [
                if (list[i]>list[i+1])
                [
                    list exchange(i,i+1).
                    swapped := true.
                ]
            ].
        ].

        ^ list
    ]
}

program =
[
    var list := (3, 5, 1, 9, 7, 6, 8, 2, 4 ).

    console printLine("before:", list).
    console printLine("after :", list cocktailSort).
].
