text
decode(list l)
{
    integer c, e, i;
    text al, s;

    al = "abcdefghijklmnopqrstuvwxyz";
    for (i, e in l) {
        s = insert(s, -1, c = al[e]);
        al = insert(delete(al, e), 0, c);
    }

    s;
}

list
encode(text s)
{
    integer c, e;
    text al;
    list l;

    al = "abcdefghijklmnopqrstuvwxyz";
    while (c = initial(s)) {
        s = delete(s, 0);
        l.append(e = place(al, c));
        al = insert(delete(al, e), 0, c);
    }

    l;
}

integer
main(void)
{
    integer i;
    text s;

    for (i, s in list("broood", "bananaaa", "hiphophiphop")) {
        list l;

        l = encode(s);
        l.ucall(o_, 1, " ");
        o_(": ", decode(l), "\n");
    }

    0;
}
